buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = spineRepository }
        maven { url = spineSnapshotRepository }
    }
    dependencies {
        classpath group: 'com.google.guava', name: 'guava', version: '19.0'
        classpath ('com.google.protobuf:protobuf-gradle-plugin:0.7.7') {
            // exclude old Guava version
            exclude group: 'com.google.guava'
        }
        // TODO:08-11-16:dmytro.dashenkov: Switch dependencies when plugins v0.6.0 are released.
       // classpath group: "org.spine3.tools", name: "protobuf-plugin", version: spineVersion
        classpath group: "org.spine3.tools", name: "protobuf-plugin", version: '1.4.11'
    }
    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.spine3.tools.protobuf-plugin'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = spineRepository
    }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: "org.spine3", name: "client", version: "$spineVersion", changing: true

    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

protobuf {
    generatedFilesBaseDir = "$projectDir/generated";
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${task.sourceSet.name}.desc"
            task.descriptorSetOptions.includeImports = true
        }
    }
}

idea.module {
    generatedSourceDirs += file("$projectDir/generated/test/java")
}

sourceSets {
    test {
        java.srcDirs = ["$projectDir/src/test/java", "$projectDir/generated/test/java"]
        proto.srcDirs = ["$projectDir/src/test/proto"]
        resources.srcDirs = ["$projectDir/generated/test/resources"]
    }
}
