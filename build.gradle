def SPINE_VERSION = '0.6.5-SNAPSHOT'

buildscript {
    ext {
        protobufGradlePluginVersion = '0.8.0' // Make sure the same constant is used in the `plugins` block below.
        guavaVersion = '20.0'
    }
}

allprojects {
    group = 'org.spine3.tools'
    version = SPINE_VERSION

    project.ext {
        spineRepository = 'http://maven.teamdev.com/repository/spine'
        spineSnapshotRepository = 'http://maven.teamdev.com/repository/spine-snapshots'
        spineVersion = SPINE_VERSION
        protobufVersion = '3.0.0'
        slf4JVersion = '1.7.21'
        jUnitVersion = '4.12'
    }
}

final String CREDENTIALS_FILE_PATH = "credentials.properties"
def repositoryUserName = null
def repositoryUserPassword = null
Properties properties = new Properties()
File credentialsFile = file(CREDENTIALS_FILE_PATH)
if (credentialsFile.exists()) {
    properties.load(credentialsFile.newDataInputStream())
    repositoryUserName = properties.getProperty("user.name")
    repositoryUserPassword = properties.getProperty("user.password")
}

ext {
    publishPlugin = "$rootDir/scripts/publish.gradle"

    credentialsPropertyFile = 'credentials.properties'
    projectsToPublish = ['protobuf-plugin', 'reflections-plugin']
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.7;
    targetCompatibility = 1.7;

    // Set Java home to point to JDK7 in gradle.properties file.
    //
    // For Mac OS X, it looks like this:
    //
    // # suppress inspection "UnusedProperty"
    // org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk1.7.0_80.jdk/Contents/Home/

    repositories {
        mavenCentral()
    }

    dependencies {
        // NOTE: do not add `gradleApi()` or `localGroovy()` dependencies here to avoid problems.
        // They depend on an old Guava version. Add them only in sub-projects which need these dependencies.

        compile "com.google.protobuf:protobuf-java:$protobufVersion"
        compile group: 'org.slf4j', name:'slf4j-api', version: slf4JVersion

        //Use jdk14 bindings for test purposes only.
        testCompile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4JVersion
        testCompile(group: 'junit', name: 'junit', version: jUnitVersion)
    }

    final String SRC_DIR = "$projectDir/plugin/src"

    sourceSets {
        main {
            groovy.srcDirs += "$SRC_DIR/main/groovy"
            resources.srcDirs += "$SRC_DIR/main/resources"
        }
        test {
            groovy.srcDirs += "$SRC_DIR/test/groovy"
            resources.srcDirs += "$SRC_DIR/test/resources"
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }
}

repositories {
    mavenCentral()
}

apply from: publishPlugin
