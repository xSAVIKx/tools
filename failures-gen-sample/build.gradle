group 'org.spine3.tools'
version '1.0'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = "${project.MAVEN_REPOSITORY_URL}"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-java:3.0.0-beta-2'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
        classpath "org.spine3.tools:protobuf-plugin:$PROTOBUF_PLUGIN_VERSION"
    }
    configurations.all({
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    })
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.spine3.tools.protobuf-plugin'

repositories {
    mavenLocal()
    maven {
        url = "$MAVEN_REPOSITORY_URL"
    }
    mavenCentral()
}

dependencies {
    compile "org.spine3:server:$SPINE_VERSION"
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-2'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/generated";
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-2'
    }
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${task.sourceSet.name}.desc"
            task.descriptorSetOptions.includeImports = true
        }
    }
}

idea.module {
    generatedSourceDirs += file("$projectDir/generated/main/java")
    generatedSourceDirs += file("$projectDir/generated/main/spine")
}

sourceSets {
    main {
        proto.srcDirs = ["$projectDir/src/main/proto"]
        java.srcDirs = ["$projectDir/generated/main/java",
                        "$projectDir/generated/main/spine",
                        "$projectDir/src/main/java"]
        resources.srcDirs = ["$projectDir/generated/main/resources"]
    }
}
